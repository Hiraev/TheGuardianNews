apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.koin:koin-gradle-plugin:$koin_version"
    }
}

android {
    compileSdkVersion android_compile_sdk_version

    defaultConfig {
        applicationId application_id
        minSdkVersion android_min_sdk_version
        targetSdkVersion android_target_sdk_version
        versionCode version_code
        versionName version_name
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            keyAlias signing_key_alias_release
            keyPassword signing_key_password_release
            storeFile file(signing_store_file_path_release)
            storePassword signing_store_password_release
        }
        debug {
            keyAlias signing_key_alias_debug
            keyPassword signing_key_password_debug
            storeFile file(signing_store_file_path_debug)
            storePassword signing_store_password_debug
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
            debuggable true
            applicationIdSuffix ".debug"
            signingConfig signingConfigs.debug

            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt')
        }
        release {
            minifyEnabled true
            shrinkResources true

            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguarg-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }

    sourceSets {
        main {
            java.srcDirs = ['src/main/kotlin']
        }
        test {
            java.srcDirs = ['src/test/kotlin']
        }
        androidTest {
            java.srcDirs = ['src/androidTest/kotlin']
        }
    }

    // For apk
    splits {
        density.enable true
        abi.enable true
        abi.universalApk false
        language.enable true
    }

    // For app bundle
    bundle {
        language {
            // Specifies that the app bundle should not support
            // configuration APKs for language resources. These
            // resources are instead packaged with each base and
            // dynamic feature APK.
            enableSplit = true
        }
        density {
            // This property is set to true by default.
            enableSplit = true
        }
        abi {
            // This property is set to true by default.
            enableSplit = true
        }
    }
}

//TODO check all dependencies
dependencies {
    implementation project(':domain-model')
    implementation project(':interactors-api')
    implementation project(':di-container')

    implementation "androidx.appcompat:appcompat:$androidx_appcompat_version"
    implementation "androidx.constraintlayout:constraintlayout:$androidx_constraintlayout_version"
//    implementation "androidx.core:core-ktx:$androidx_core_ktx"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlin_coroutines_version"
//    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlin_coroutines_version"
    implementation "ru.terrakok.cicerone:cicerone:$cicerone_version"

    //Koin
    implementation "org.koin:koin-core:$koin_version"
    implementation "org.koin:koin-core-ext:$koin_version"
    implementation "org.koin:koin-androidx-scope:$koin_version"
    implementation "org.koin:koin-androidx-viewmodel:$koin_version"
    implementation "org.koin:koin-androidx-ext:$koin_version"


    testImplementation "org.koin:koin-test:$koin_version"
    testImplementation "junit:junit:$junit_version"
    androidTestImplementation "androidx.test.ext:junit:$androidx_junit_version"
    androidTestImplementation "androidx.test.espresso:espresso-core:$androidx_espresso_version"
}
